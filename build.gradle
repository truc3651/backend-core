plugins {
    id 'java-library'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id "com.diffplug.spotless" version '6.25.0'
    id 'maven-publish'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2023.0.0"
    }
}

dependencies {
    // Spring Boot starters
    api 'org.springframework.boot:spring-boot-starter-web'
    api 'org.springframework.boot:spring-boot-starter-security'
    api 'org.springframework.boot:spring-boot-starter-data-jpa'
    api 'org.springframework.boot:spring-boot-starter-validation'

    // Lombok for reducing boilerplate
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    api 'com.fasterxml.jackson.core:jackson-databind'

    // OpenFeign
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:4.1.0'

    // JWT
    api 'io.jsonwebtoken:jjwt-api:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'

    // AWS
    implementation(platform("software.amazon.awssdk:bom:2.27.21"))
    implementation 'software.amazon.awssdk:secretsmanager'

    // Commons lang
    implementation("org.apache.commons:commons-lang3:3.20.0")

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

bootJar {
    enabled = false
}

jar {
    enabled = true
    archiveClassifier = ''
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'Backend Core Library'
                description = 'Shared core library for backend microservices'
                url = 'https://github.com/truc3651/backend-core'
            }
        }
    }
    repositories {
        maven {
            url = uri("https://maven.pkg.github.com/${System.getenv("GITHUB_REPOSITORY")}")
            credentials {
                username = System.getenv("USERNAME")
                password = System.getenv("TOKEN")
            }
        }
    }
}

spotless {
    format 'misc', {
        target '*.gradle', '.gitattributes', '.gitignore'
        trimTrailingWhitespace()
        indentWithSpaces(3)
        endWithNewline()
        setEncoding('utf-8')
    }
    java {
        removeUnusedImports()
        googleJavaFormat('1.15.0').reflowLongStrings().reorderImports(false)
        indentWithSpaces(2)
        trimTrailingWhitespace()
        importOrder( '\\#', 'java.' , 'org.', 'com.')
        endWithNewline()
        toggleOffOn()
    }
}
